plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.3'
}

group = 'com.litclub'
version = '0.1.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

javafx {
    version = "24.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation('com.google.code.gson:gson:2.8.9')
    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.1")
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.20.1')
    implementation('org.json:json:20250517')
}

application {
    mainClass = 'com.litclub.Launcher'
    applicationDefaultJvmArgs = [
            '--enable-native-access=javafx.graphics,javafx.web,javafx.media'
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'LitClub'
    }

    jpackage {
        appVersion = project.version

        // Detect OS and set installer type accordingly
        def osName = System.getProperty('os.name').toLowerCase()
        if (osName.contains('linux')) {
            installerType = 'deb'
        } else if (osName.contains('windows')) {
            installerType = 'exe'
        }

        // Platform-specific installer options
        if (osName.contains('linux')) {
            installerOptions = [
                    '--linux-shortcut',
                    '--linux-menu-group', 'Utility',
                    '--linux-app-category', 'utils'
            ]

            imageOptions = [
                    '--icon', file('src/main/resources/com/litclub/ui/icons/litclub.png').absolutePath
            ]
        } else if (osName.contains('windows')) {
            installerOptions = [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut'
            ]

            imageOptions = [
                    '--icon', file('src/main/resources/com/litclub/ui/icons/litclub.ico').absolutePath
            ]
        }

        jvmArgs = [
                '--enable-native-access=javafx.graphics,javafx.web,javafx.media'
        ]
    }
}